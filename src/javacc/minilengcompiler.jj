//*****************************************************************
// File:   MiniLengCompiler.jj
// Author: Andrés Gavín Murillo 716358
// Date:   14/4/2019
// Coms:   Procesadores de lenguajes - Compilador de MiniLengCompiler V2.2
//         JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
//*****************************************************************
options{    static = true;
    ignore_case = true;}PARSER_BEGIN(MiniLengCompiler)

package javacc;

import lib.TablaVeces;
import java.io.FileInputStream;

public class MiniLengCompiler {
    static boolean correcto = true;
    static boolean panic_mode = false;
    
    public static void main(String args []) throws ParseException {
        System.out.println("Compilador de MiniLengCompiler V2.2 Autor: Andrés Gavín Murillo 716358. Ayuda: ./compilador -h\n");
        
        Boolean verbose = false;
        MiniLengCompiler compiler;
        TablaVeces tabla = new TablaVeces();

        // Lanzar compilador
        if (args.length == 0) { // Sin argumentos
            System.out.println("Uso: ./compilador [argumentos] [archivo]");
            return ;
        }
        else { // Opción ayuda
            if (args[0].equals("-h")) {
                System.out.println("Uso: ./compilador [argumentos] [archivo]");
                System.out.println("     [] -> opcional");
                System.out.println("Argumentos:");
                System.out.println("    -h  -> Muestra la ayuda");
                System.out.println("    -p  -> Panic mode");
                System.out.println("    -v  -> Muestra información adicional");
                return ;
            }
            else {
                String file_name = null;
                
                for (int i=0; i<args.length; i++) {
                    if (args[i].equals("-p"))
                        panic_mode = true;
                    else if (args[i].equals("-v"))
                        verbose = true;
                    else
                        file_name = args[i];
                }

                if (file_name != null) { 
	               try {
	                   compiler = new MiniLengCompiler(new FileInputStream(file_name));
	               }
	               catch (java.io.FileNotFoundException e) {
	                   System.out.println("Fichero " + file_name + " no encontrado.");
	                   return ;
	               }
                }
	            else {
	               System.out.println("Fichero no encontrado.");
                   return ;
                }
            }

            try { 
                MiniLengCompiler.programa(verbose);
                if (correcto)
                    System.out.println("Compilación correcta.");
            }
            catch (Error e) { // Error léxico
                System.out.println("ERROR LÉXICO (<" + SimpleCharStream.getBeginLine() + ", " + 
                    SimpleCharStream.getBeginColumn() + ">) : símbolo no reconocido: <" +
                    SimpleCharStream.GetImage().charAt(MiniLengCompilerTokenManager.curLexState) + ">");
                
                return ;
            }
        }
    }

    private static void panic(Exception e, String msg, int kind) {
        Token lastRead = MiniLengCompiler.getNextToken();

        while (token.kind != kind && token.kind != EOF)
            token = MiniLengCompiler.getNextToken();

        String error_msg = "AVISO SINTÁCTICO (<" + lastRead.beginLine + ", " + 
            lastRead.beginColumn + ">) : <";
        if (kind != tFIN)
            error_msg += "Símbolo obtenido: '" + lastRead.image + "'. ";
        error_msg += msg + " (PANIC MODE)>";
        
        System.out.println(error_msg);
    }

    private static void error_sintactico(Exception e, String msg) {
        correcto = false;
        Token lastRead = MiniLengCompiler.getNextToken();
        
        System.out.println("ERROR SINTÁCTICO (<" + lastRead.beginLine + ", " + 
            lastRead.beginColumn + ">) : <Símbolo obtenido: '" + lastRead.image +
            "'. " + msg + ">");
    }

    private static void error_sintactico(Exception e) {
        error_sintactico(e, "");
    }
}
// Análisis léxico
PARSER_END(MiniLengCompiler)SKIP :{  " "| "\r"| "\t"| "\n"
| "%" : COMENTARIO}

< COMENTARIO > SKIP :
{
  "\n" : DEFAULT
}

< COMENTARIO > MORE :
{
  < ~[] >
}

TOKEN : /* Estructuras */
{  < tPROGRAMA : "programa" >
    {
        TablaVeces.incrementarValor(0);
    }
| < tPRINCIPIO : "principio" >
    {
        TablaVeces.incrementarValor(1);
    }
| < tFIN : "fin" >
    {
        TablaVeces.incrementarValor(2);
    }
| < tSI : "si" >
    {
        TablaVeces.incrementarValor(3);
    }
| < tENT : "ent" >
    {
        TablaVeces.incrementarValor(4);
    }
| < tSI_NO : "si_no" >
    {
        TablaVeces.incrementarValor(5);
    }
| < tFSI : "fsi" >
    {
        TablaVeces.incrementarValor(6);
    }
| < tMQ : "mq" >
    {
        TablaVeces.incrementarValor(7);
    }
| < tFMQ : "fmq" >
    {
        TablaVeces.incrementarValor(8);
    }
| < tACCION : "accion" >
    {
        TablaVeces.incrementarValor(9);
    }
| < tPC : ";" >
    {
        TablaVeces.incrementarValor(10);
    }
| < tC : "," >
    {
        TablaVeces.incrementarValor(11);
    }
}

TOKEN : /* Tipos de datos */
{
  < tVAR : "var" >
    {
        TablaVeces.incrementarValor(12);
    }
| < tENTERO : "entero" >
    {
        TablaVeces.incrementarValor(13);
    }
| < tBOOLEANO : "booleano" >
    {
        TablaVeces.incrementarValor(14);
    }
| < tCARACTER : "caracter" >
    {
        TablaVeces.incrementarValor(15);
    }
| < tVAL : "val" >
    {
        TablaVeces.incrementarValor(16);
    }
| < tREF : "ref" >
    {
        TablaVeces.incrementarValor(17);
    }
}

TOKEN : /* Operadores lógicos */
{
  < tAND : "and" >
    {
        TablaVeces.incrementarValor(18);
    }
| < tOR : "or" >
    {
        TablaVeces.incrementarValor(19);
    }
| < tNOT : "not" >
    {
        TablaVeces.incrementarValor(20);
    }
| < tMAYOR : ">" >
    {
        TablaVeces.incrementarValor(21);
    }
| < tMENOR : "<" >
    {
        TablaVeces.incrementarValor(22);
    }
| < tIGUAL : "=" >
    {
      TablaVeces.incrementarValor(23);
    }
| < tMAI : ">=" >
    {
        TablaVeces.incrementarValor(24);
    }
| < tMEI : "<=" >
    {
        TablaVeces.incrementarValor(25);
    }
| < tNI : "<>" >
    {
        TablaVeces.incrementarValor(26);
    }
| < tTRUE : "true" >
    {
        TablaVeces.incrementarValor(27);
    }
| < tFALSE : "false" >
    {
        TablaVeces.incrementarValor(28);
    }
}

TOKEN : /* Operaciones */
{
  < tESCRIBIR : "escribir" >
    {
        TablaVeces.incrementarValor(29);
    }
| < tLEER : "leer" >
    {
        TablaVeces.incrementarValor(30);
    }
| < tENTACAR : "entacar" >
    {
        TablaVeces.incrementarValor(31);
    }
| < tCARAENT : "caraent" >
    {
        TablaVeces.incrementarValor(32);
    }
}
TOKEN : /* Operadores aritméticos */{
  < tMOD : "mod" >
    {
        TablaVeces.incrementarValor(33);
    }
| < tDIV : "div" | "/" >
    {
        TablaVeces.incrementarValor(34);
    }
| < tOPAS : ":=" >
    {
        TablaVeces.incrementarValor(35);
    }
| < tSUMA : "+" >
    {
        TablaVeces.incrementarValor(36);
    }
| < tRESTA : "-" >
    {
        TablaVeces.incrementarValor(37);
    }
| < tMULTIPLICA : "*" >
    {
        TablaVeces.incrementarValor(38);
    }
| < tAP : "(" >
    {
        TablaVeces.incrementarValor(39);
    }
| < tCP : ")" >
    {
        TablaVeces.incrementarValor(40);
    }}TOKEN :{  < tCONSTENTERA : (< DIGITO >)+ >
    {
        TablaVeces.incrementarValor(41);
    }| < #DIGITO : [ "0"-"9" ] >
| < tCONSTCHAR : ["\""] ~["\""] ["\""] >
    {
        TablaVeces.incrementarValor(42);
    }
| < tCONSTCAD : ["\""] (~["\""])+ ["\""] >
    {
        TablaVeces.incrementarValor(43);
    }
| < tIDENTIFICADOR : (< LETRA > | (< LETRA > | "_") (< LETRA > | < DIGITO > | "_")*
    (< LETRA > | < DIGITO >)) >
    {
        TablaVeces.incrementarValor(44);
    }
| < #LETRA : [ "A"-"Z" ] >}

// Análisis sintáctico

void punto_y_coma() :
{}
{
    try { 
        <tPC>
    }
    catch (Exception e) { // Error sintáctico
        if (panic_mode)
            panic(e, "Se esperaba ';'", tPC);
        else
            error_sintactico(e, "Se esperaba ';'");
    }
}

void identificador() :
{}
{
    try { 
        <tIDENTIFICADOR>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba un identificador");
    }
}
void programa(Boolean verbose) :
{}
{
    try { 
	    <tPROGRAMA> identificador() punto_y_coma() declaracion_variables() declaracion_acciones()
	     bloque_sentencias() ( <EOF> )?
	        {
	            if (verbose && correcto)
	                TablaVeces.print();
	            return;
	        }
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba la declaración del programa");
    }
}

void declaracion_variables() :
{}
{
    try { 
        ( declaracion() punto_y_coma() )*
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void declaracion() :
{}
{
    try { 
        tipo_variables() identificadores()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void tipo_variables() :
{}
{
    try { 
        <tENTERO> | <tCARACTER> | <tBOOLEANO>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba el tipo de varibale");
    }
}

void identificadores() :
{}
{
    try { 
        identificador() ( <tC> identificador() )*
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void declaracion_acciones() :
{}
{
    try { 
        ( declaracion_accion() )*
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void declaracion_accion() :
{}
{
    try { 
        cabecera_accion() punto_y_coma() declaracion_variables()
         declaracion_acciones() bloque_sentencias()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void cabecera_accion() :
{}
{
    try { 
        <tACCION> identificador() parametros_formales()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba la declaración de una acción");
    }
}

void ap() :
{}
{
    try { 
        <tAP>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba '('");
    }
}

void cp() :
{}
{
    try { 
        <tCP>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba ')'");
    }
}

void parametros_formales() :
{}
{
    try { 
        ( ap() ( parametros() )* cp() )?
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void lista_parametros() :
{}
{
    try { 
        identificadores() ( punto_y_coma() parametros() )?
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void parametros() :
{}
{
    try { 
        clase_parametros() tipo_variables() lista_parametros()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void clase_parametros() :
{}
{
    try { 
        <tVAL> | <tREF>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba la clase de parámetro");
    }
}

void principio() :
{}
{
    try { 
        <tPRINCIPIO>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba la declaración del principio del bloque");
    }
}

void bloque_sentencias() :
{}
{
    try { 
        principio() lista_sentencias() <tFIN>
    }
    catch (Exception e) { // Error sintáctico
        if (panic_mode)
            panic(e, "Se esperaba la declaración del fin del bloque", tFIN);
        else
            error_sintactico(e, "Se esperaba la declaración del fin del bloque");
    }
}

void lista_sentencias() :
{}
{
    try { 
        ( sentencia() )+
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void sentencia() :
{}
{
    try { 
        leer() punto_y_coma()
	    | escribir() punto_y_coma()
	    | identificacion()
	    | seleccion()
	    | mientras_que()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void leer() :
{}
{
    try { 
        <tLEER> ap() lista_asignables() cp()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void lista_asignables() :
{}
{
    try { 
        identificadores()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void escribir() :
{}
{
    try { 
        <tESCRIBIR> ap() lista_escribibles() cp()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void constcad() :
{}
{
    try { 
        <tCONSTCAD>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba una cadena de texto");
    }
}

void lista_escribibles() :
{}
{
    try { 
        ( constcad() | expresion() ) ( <tC> ( constcad() | expresion() ) )*
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void identificacion() :
{}
{
    try { 
        identificador() ( asignacion() | invocacion_accion() )
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void asignacion() :
{}
{
    try { 
        <tOPAS> expresion() punto_y_coma()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba una asignación ':='");
    }
}

void invocacion_accion() :
{}
{
    try { 
        ( argumentos() )? punto_y_coma()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void fmq() :
{}
{
    try { 
        <tFMQ>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba el fin del bloque MQ");
    }
}

void mientras_que() :
{}
{
    try { 
        <tMQ> expresion() lista_sentencias() fmq()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void ent() :
{}
{
    try { 
        <tENT>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba 'ent'");
    }
}

void fsi() :
{}
{
    try { 
        <tFSI>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba el fin del bloque SI");
    }
}

void seleccion() :
{}
{
    try { 
        <tSI> expresion() ent() lista_sentencias() ( <tSI_NO> lista_sentencias() )? fsi()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void argumentos() :
{}
{
    try { 
        ap() ( lista_expresiones() )? cp()
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void lista_expresiones() :
{}
{
    try { 
        expresion() ( <tC> expresion() )*
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void expresion() :
{}
{
    try { 
        expresion_simple() ( operador_relacional() expresion_simple() )?
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void operador_relacional() :
{}
{
    try { 
        <tMAYOR> | <tMENOR> | <tIGUAL> | <tMAI> | <tMEI> | <tNI>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba un operador relacional");
    }
}

void expresion_simple() :
{}
{
    try { 
        termino() ( operador_aditivo() termino() )*
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void operador_aditivo() :
{}
{
    try { 
        <tSUMA> | <tRESTA> | <tOR>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba un operador aditivo");
    }
}

void termino() :
{}
{
    try { 
        factor() ( operador_multiplicativo() factor() )*
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e);
    }
}

void operador_multiplicativo() :
{}
{
    try { 
        <tMOD> | <tDIV> | <tMULTIPLICA> | <tAND>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba un operador multiplicativo");
    }
}

void factor() :
{}
{
    try { 
        <tRESTA> factor()
	    | <tNOT> factor()
	    | ap() expresion() cp()
	    | <tENTACAR> ap() expresion() cp()
	    | <tCARAENT> ap() expresion() cp()
	    | identificador()
	    | <tCONSTENTERA>
	    | <tCONSTCHAR>
	    | <tTRUE>
	    | <tFALSE>
    }
    catch (Exception e) { // Error sintáctico
        error_sintactico(e, "Se esperaba un factor");
    }
}
